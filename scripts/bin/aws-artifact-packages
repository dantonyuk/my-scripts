#!/bin/zsh

if [ -z ${AWS_PROFILE} ]; then
    source aws-profile
fi

AWS_REGION=${AWS_REGION:-$(aws configure list | grep region | awk '{print $2}')}
AWS_ACCOUNT_ID=$(aws sts get-caller-identity --query 'Account' --output text)

DOMAINS=$(
    aws codeartifact list-domains \
        --query 'domains[].[name]' \
        --output text)
if [ -z "${DOMAINS}" ]
then
    echo "No CodeArtifact domains found in the account"
    exit 1
elif [ $(wc -l <<< "${DOMAINS}") -eq 1 ]
then
    DOMAIN=${DOMAINS}
else
    DOMAIN=$(echo ${DOMAINS} | fzf --prompt="Pick a domain> ")
fi

if [ -z "${DOMAIN}" ]
then
    exit
fi

echo "Domain: ${DOMAIN}"

REPOSITORIES=$(
    aws codeartifact list-repositories-in-domain \
        --domain ${DOMAIN} \
        --query 'repositories[].[name]' \
        --output text)
if [ -z "${REPOSITORIES}" ]
then
    echo "No CodeArtifact repositories found in the domain ${DOMAIN}"
    exit 1
elif [ $(wc -l <<< "${REPOSITORIES}") -eq 1 ]
then
    REPOSITORY=${REPOSITORIES}
else
    REPOSITORY=$(echo ${REPOSITORIES} | fzf --prompt="Pick a repository> ")
fi

if [ -z "${REPOSITORY}" ]
then
    exit
fi

echo "Repository: ${REPOSITORY}"

REPOSITORY_URL="https://${AWS_REGION}.console.aws.amazon.com/codesuite/codeartifact/d/${AWS_ACCOUNT_ID}/${DOMAIN}/r/${REPOSITORY}/p/maven"

if [ -z "${CODEARTIFACT_AUTH_TOKEN}" ]
then
    export CODEARTIFACT_AUTH_TOKEN=$(
        aws codeartifact get-authorization-token \
            --domain ${DOMAIN} \
            --query authorizationToken --output text)
fi

export REPOSITORY_ENDPOINT=$(
    aws codeartifact get-repository-endpoint \
        --domain ${DOMAIN} \
        --repository ${REPOSITORY} \
        --format maven \
        --output text \
        --query 'repositoryEndpoint')

PACKAGES=$(
    aws codeartifact list-packages --domain ${DOMAIN} --repository ${REPOSITORY} --no-paginate | \
    jq -r '.packages[] | .namespace + ":" + .package')
if [ -z "${PACKAGES}" ]
then
    echo "No CodeArtifact packages found in the repository ${REPOSITORY}"
    exit 1
fi

PACKAGES=( $(echo ${PACKAGES} | \
    fzf --prompt="Pick a package> " --layout reverse --pointer='â–¶' \
        --header "Enter: View package(s) versions | Ctrl-O: Open in browser | Ctrl-Y: Yank version | Ctrl-U: Yank URL | Ctrl-A: (Un)select all" \
        --header-first \
        --multi \
        --bind ctrl-a:toggle-all \
        --bind 'ctrl-y:execute-silent(echo -n {} | pbcopy)' \
        --bind "ctrl-o:execute-silent(open '${REPOSITORY_URL}/{1}/{2}/versions?region=${AWS_REGION}')" \
        --bind "ctrl-u:execute-silent(echo '${REPOSITORY_URL}/{1}/{2}/versions?region=${AWS_REGION}' | pbcopy)" \
        --delimiter ':' \
        --preview-window='down:50%' \
        --preview "aws codeartifact list-package-versions \
            --domain ${DOMAIN} --repository ${REPOSITORY} \
            --namespace {1} --package {2} \
            --format maven --output json \
            | jq -r '.versions[] | .version + "'": "'" + .status'"))
if [ -z "${PACKAGES}" ]
then
    exit
fi

VERSIONS=($(
    (
        for FULL_PACKAGE in ${PACKAGES[@]}
        do
            PACKAGE_PARTS=(${(s/:/)${FULL_PACKAGE}})
            NAMESPACE=$PACKAGE_PARTS[1]
            PACKAGE=$PACKAGE_PARTS[2]
            NAMESPACE_PATH=${NAMESPACE_PATH:gs|.|/|}
            PACKAGE_DIR="${NAMESPACE_PATH}/${PACKAGE}/${}"

            aws codeartifact list-package-versions \
                --domain ${DOMAIN} \
                --repository ${REPOSITORY} \
                --namespace ${NAMESPACE} \
                --package ${PACKAGE} \
                --format maven | \
            jq -r '.versions[] | "'${FULL_PACKAGE}:'" + .version'
        done
    ) | \
    fzf --prompt="Pick a version(s)> " --layout reverse \
        --header "Enter: Print version(s) | Del: Delete version(s) | Ctrl-G: Download | Ctrl-O: Open in browser | Ctrl-Y: Yank version | Ctrl-U: Yank URL | Ctrl-A: (Un)select all" \
        --header-first \
        --multi \
        --delimiter ':' \
        --bind ctrl-a:toggle-all \
        --bind 'ctrl-y:execute-silent(echo -n {} | pbcopy)' \
        --bind "ctrl-h:execute(echo curl -u 'aws:$CODEARTIFACT_AUTH_TOKEN' '${REPOSITORY_ENDPOINT}(echo "{1}" | tr . /)/{2}/{3}/{2}-{3}.jar')" \
        --bind "ctrl-g:execute(
            mkdir -p {2}-{3}
            cd {2}-{3}
            ASSETS=(\$(
                aws codeartifact list-package-version-assets \
                    --domain ${DOMAIN} --repository ${REPOSITORY} --format maven \
                    --namespace {1} \
                    --package {2} \
                    --package-version {3} \
                    --query 'assets[*].[name]' --output text))
            for ASSET in \${ASSETS[@]}
            do
                echo "'"'"Downloading \${ASSET}..."'"'"
                ASSET_PATH=\$(echo {1} | tr . /)/{2}/{3}/\${ASSET}
                curl -sLO -u 'aws:$CODEARTIFACT_AUTH_TOKEN' '${REPOSITORY_ENDPOINT}'\${ASSET_PATH}
            done
            cd -)" \
        --bind "ctrl-o:execute-silent(open '${REPOSITORY_URL}/{1}/{2}/v/{3}?region=${AWS_REGION}')" \
        --bind "ctrl-u:execute-silent(echo '${REPOSITORY_URL}/{1}/{2}/v/{3}?region=${AWS_REGION}' | pbcopy)" \
        --preview '
            echo -e "Assets\n======\n";
            aws codeartifact list-package-version-assets \
                --domain '${DOMAIN}' --repository '${REPOSITORY}' --format maven \
                --namespace {1} \
                --package {2} \
                --package-version {3} \
                --query '"'"'assets[*].[name]'"'"' --output text 2>&1;
            echo -e "\nPOM file\n========\n";
            CODEARTIFACT_AUTH_TOKEN=${CODEARTIFACT_AUTH_TOKEN} REPOSITORY_ENDPOINT=${REPOSITORY_ENDPOINT} aws-artifact-pom {}' \
        --expect del
))

if [[ "$VERSIONS[1]" == "del" ]]
then
    shift VERSIONS
    for PACKAGE_VERSION in ${VERSIONS[@]}
    do
        VERSION_PARTS=(${(s/:/)${PACKAGE_VERSION}})
        NAMESPACE=$VERSION_PARTS[1]
        PACKAGE=$VERSION_PARTS[2]
        VERSION=$VERSION_PARTS[3]

        echo "Deleting ${PACKAGE_VERSION}..."
        aws codeartifact delete-package-versions \
            --domain ${DOMAIN} \
            --repository ${REPOSITORY} \
            --format maven \
            --namespace ${NAMESPACE} \
            --package ${PACKAGE} \
            --versions ${VERSION} \
            --output text
    done
else
    echo ${VERSIONS[@]}
fi
